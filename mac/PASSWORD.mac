ROUTINE PASSWORD
PASSWORD ; Программа проверки паролей
#include %occInclude
CHECK(Username,Password) PUBLIC {
	if '(($length(Password)>6)&&(Password?.E1.N.E)&&(Password?.E1.(1.A,1.P).E)) quit $$$ERROR($$$GeneralError,"Пароль не соответствует стандарту PCI_DSS_v3.2")
	set Remember=4 ;Количество последних паролей, которых нельзя использовать по PCI-DSS 
	set GlobRef="^PASSWORDLIST" ; Имя глобальной ссылки
  	set PasswordHash=$System.Encryption.SHA1Hash(Password)
	if $d(@GlobRef@(Username,"hash",PasswordHash)){
	   	quit $$$ERROR($$$GeneralError,"Этот пароль уже использовался")
	}
	set hor=""
  	for i=1:1 {
	  	; Обходим узлы по хронологии от новых к старым
	  	set hor=$order(@GlobRef@(Username,"datetime",hor),-1) 
	  	quit:hor=""
	  	; Удаляем старый сверх лимита
	  	if i>(Remember-1) {
		  	set hash=$g(@GlobRef@(Username,"datetime",hor))
		  	kill @GlobRef@(Username,"datetime",hor)
		  	kill:hash'="" @GlobRef@(Username,"hash",hash)
	  	}
  	}
  	; Сохраним текущий
  	set @GlobRef@(Username,"hash",PasswordHash)=$h
  	set @GlobRef@(Username,"datetime",$h)=PasswordHash
  	quit $$$OK
}
