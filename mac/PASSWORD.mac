PASSWORD ; password check routine
#include %occInclude
#include %occMessages
CHECK( user, passw ) PUBLIC {
	
	s match = $length(passw)>6 && ( passw?.E1.N.E ) && ( passw?.E1.(1.A,1.P).E )
	 
	/// localization => l10n, see https://habrahabr.ru/company/intersystems/blog/144312/ 
	#define l10n(%str) $$$Text(%str,"PASSWORD")
	s errmsg = $$$l10n("Пароль не соответствует стандарту PCI_DSS_v3.2")
	
	Q:'match $$$ERROR( $$$GeneralError, errmsg )

   	s hash = $System.Encryption.SHA1Hash( passw )
   	
   	/// we need storage
	#def1arg gl(%args) ^PASSWORDLOG(%args)
	
	s used = $d( $$$gl(user,"hash",hash) )
	s errmsg = $$$l10n("Этот пароль уже использовался")
	Q:used $$$ERROR($$$GeneralError,errmsg)

  	#; write passw log
  	s dt=$h //datetime
  	s $$$gl(user,"dt",dt)=hash // last password
	s $$$gl(user,"hash",hash)=dt // index for used

	#; clear passwords log - 
	/// require by standard PCI-DSS 
	#define PasswNotRepeatTimes 4 
	s i=0, dt=""
  	for { s i=i+1, dt=$o($$$gl(user,"dt",dt), 1, hash) Q:dt=""

	  	if i'>$$$PasswNotRepeatTimes continue
		#; remove oldest hashes
	  	k $$$gl(user,"dt",dt) // clear hash
		k:hash'="" $$$gl(user,"hash",hash) //clear hash index
		// hash'="" How it possible?
	}
	
  	Q $$$OK
}
